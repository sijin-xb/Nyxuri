import QtQuick
import QtQuick.Controls
import QtQuick.Controls.Material
import QtQuick.Layouts
import QtQuick.Window
import qs.shared.theme
import qs.app.services
import qs.shared.controls
import qs.shared.i18n

StyledFlickable {
    id: root

    clip: true
    contentWidth: width
    contentHeight: contentColumn.y + contentColumn.implicitHeight + 24

    Component.onCompleted: NiriConfigService.refresh()

    readonly property real pageContentWidth: 600
    property var parentModal: null
    property var pendingDeleteTemplate: null

    function requestTemplateDeletion(template) {
        root.pendingDeleteTemplate = template;
        templateDialog.open();
    }

    function closeChildWindows() {
        templateAddWindow.dismiss();
        templateDialog.close();
    }

    component Section: ColumnLayout {
        id: section

        property string title: ""
        property string iconName: "palette"
        default property alias content: body.data

        Layout.fillWidth: true
        spacing: 12

        RowLayout {
            Layout.fillWidth: true
            spacing: 8

            MaterialSymbol {
                Layout.preferredWidth: 30
                Layout.preferredHeight: 30
                text: section.iconName
                iconSize: 26
                fill: 1
                color: Appearance.colors.colOnSecondaryContainer
            }

            Text {
                Layout.fillWidth: true
                text: section.title
                color: Appearance.colors.colOnSecondaryContainer
                font.family: Fonts.ui
                font.pixelSize: 18
                font.weight: Font.Medium
            }
        }

        ColumnLayout {
            id: body

            Layout.fillWidth: true
            spacing: 10
        }
    }

    component PreviewSegmentGroup: Item {
        id: previewGroup

        property bool selected: false
        property bool darkPreview: false
        property color normalFill: darkPreview ? "#5f5961" : "#e1dee2"
        property color selectedFill: Appearance.colors.colPrimary
        property color checkColor: Appearance.colors.colOnPrimary

        implicitWidth: 260
        implicitHeight: 34

        readonly property real gap: 3
        readonly property real segmentHeight: height
        readonly property real firstWidth: Math.round(width * 0.315)
        readonly property real middleWidth: Math.round(width * 0.34)
        readonly property real lastWidth: width - firstWidth - middleWidth - gap * 2

        Rectangle {
            x: 0
            y: 0
            width: previewGroup.firstWidth
            height: previewGroup.segmentHeight
            radius: height / 2
            color: previewGroup.selected ? previewGroup.selectedFill : previewGroup.normalFill
            opacity: previewGroup.selected ? 1 : 0.82
            antialiasing: true

            MaterialSymbol {
                anchors.centerIn: parent
                text: "check"
                iconSize: 16
                fill: 1
                color: previewGroup.checkColor
                visible: previewGroup.selected
            }
        }

        Rectangle {
            x: previewGroup.firstWidth + previewGroup.gap
            y: 0
            width: previewGroup.middleWidth
            height: previewGroup.segmentHeight
            radius: 5
            color: previewGroup.normalFill
            opacity: 0.82
            antialiasing: true
        }

        Rectangle {
            x: previewGroup.firstWidth + previewGroup.middleWidth + previewGroup.gap * 2
            y: 0
            width: previewGroup.lastWidth
            height: previewGroup.segmentHeight
            topLeftRadius: 0
            bottomLeftRadius: 0
            topRightRadius: height / 2
            bottomRightRadius: height / 2
            color: previewGroup.normalFill
            opacity: 0.82
            antialiasing: true
        }
    }

    component ThemePreviewCard: Item {
        id: themeCard

        required property string mode
        required property string title
        property bool darkPreview: false
        readonly property bool active: PersonalizationConfig.themeMode === mode
        readonly property color selectedAccent: Appearance.colors.colPrimary
        readonly property color selectedOnAccent: Appearance.colors.colOnPrimary
        readonly property color outerFill: active ? selectedAccent : Appearance.colors.colLayer1
        readonly property color previewSurface: darkPreview ? "#302d32" : "#fbf7f8"
        readonly property color avatarFill: darkPreview ? "#8a838c" : "#dedde1"
        readonly property color placeholderFill: darkPreview ? "#948b92" : "#dedbdf"
        readonly property color placeholderAltFill: darkPreview ? "#776f75" : "#d3d0d5"
        readonly property color waveFill: active ? selectedAccent : (darkPreview ? "#8a858e" : "#ccc8cd")
        readonly property color trackFill: darkPreview ? "#6d6870" : "#d7d3d8"
        readonly property color labelText: active ? selectedOnAccent : Appearance.colors.colOnLayer1

        signal clicked

        Layout.preferredWidth: 288
        Layout.preferredHeight: 180
        scale: cardMouse.pressed ? 0.985 : 1

        Behavior on scale {
            NumberAnimation {
                duration: 120
                easing.type: Easing.OutSine
            }
        }

        Rectangle {
            anchors.fill: parent
            radius: 13
            color: themeCard.outerFill
            border.width: 0
        }

        Rectangle {
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.bottom: parent.bottom
            height: 33
            bottomLeftRadius: 13
            bottomRightRadius: 13
            color: "transparent"

            Text {
                anchors.centerIn: parent
                text: themeCard.title
                color: themeCard.labelText
                font.family: Fonts.ui
                font.pixelSize: 14
                font.weight: Font.Medium
            }
        }

        Rectangle {
            id: previewPane

            anchors.left: parent.left
            anchors.right: parent.right
            anchors.top: parent.top
            anchors.bottom: parent.bottom
            anchors.leftMargin: 10
            anchors.rightMargin: 10
            anchors.topMargin: 10
            anchors.bottomMargin: 37
            radius: 8
            color: themeCard.previewSurface
            border.width: 0

            Rectangle {
                x: 15
                y: 12
                width: 46
                height: 46
                radius: width / 2
                color: themeCard.avatarFill
                opacity: themeCard.darkPreview ? 0.9 : 0.92
            }

            Column {
                x: 72
                y: 16
                spacing: 8

                Rectangle {
                    width: Math.min(154, previewPane.width - 92)
                    height: 18
                    radius: 5
                    color: themeCard.placeholderFill
                    opacity: themeCard.darkPreview ? 0.85 : 1
                }

                Rectangle {
                    width: Math.min(124, previewPane.width - 118)
                    height: 16
                    radius: 5
                    color: themeCard.placeholderAltFill
                    opacity: themeCard.darkPreview ? 0.95 : 1
                }
            }

            MiniMaterialWaveLine {
                x: 18
                y: 66
                width: previewPane.width - 36
                height: 18
                waveColor: themeCard.waveFill
                trackColor: themeCard.trackFill
                trackOpacity: themeCard.darkPreview ? 0.42 : 0.54
                wavePortion: 0.72
                phaseDuration: 1600
                flowing: themeCard.active
                endDotColor: themeCard.waveFill
            }

            PreviewSegmentGroup {
                x: 13
                y: previewPane.height - height - 8
                width: previewPane.width - 26
                height: 33
                selected: themeCard.active
                darkPreview: themeCard.darkPreview
                selectedFill: themeCard.selectedAccent
                checkColor: themeCard.selectedOnAccent
            }
        }

        MouseArea {
            id: cardMouse
            anchors.fill: parent
            hoverEnabled: true
            cursorShape: Qt.PointingHandCursor
            onClicked: themeCard.clicked()
        }
    }

    component SearchSelectSettingRow: Item {
        id: selectRow

        property string title: ""
        property string description: ""
        property var options: []
        property string value: ""
        property string placeholder: ""
        property string textRole: "label"
        property string valueRole: "value"
        property int fieldWidth: 240

        signal accepted(string value)

        Layout.fillWidth: true
        Layout.preferredHeight: Math.max(58, selectLabelColumn.implicitHeight + 16)

        RowLayout {
            anchors.fill: parent
            spacing: 16

            Column {
                id: selectLabelColumn

                Layout.fillWidth: true
                Layout.alignment: Qt.AlignVCenter
                spacing: 3

                Text {
                    width: parent.width
                    text: selectRow.title
                    color: Appearance.colors.colOnSurface
                    font.family: Fonts.ui
                    font.pixelSize: 15
                    font.weight: Font.Medium
                    elide: Text.ElideRight
                }

                Text {
                    width: parent.width
                    text: selectRow.description
                    color: Appearance.colors.colSubtext
                    font.family: Fonts.ui
                    font.pixelSize: 12
                    wrapMode: Text.WordWrap
                    visible: text !== ""
                }
            }

            SearchSelectMenuField {
                closeOnAccept: false
                Layout.preferredWidth: selectRow.fieldWidth
                Layout.preferredHeight: 40
                Layout.alignment: Qt.AlignVCenter
                options: selectRow.options
                value: selectRow.value
                placeholder: selectRow.placeholder
                textRole: selectRow.textRole
                valueRole: selectRow.valueRole
                onAccepted: value => selectRow.accepted(value)
            }
        }
    }

    component ToggleSettingRow: Item {
        id: toggleRow

        property string title: ""
        property string description: ""
        property bool checked: false
        property bool enabled: true

        signal toggled(bool checked)

        Layout.fillWidth: true
        Layout.preferredHeight: Math.max(58, toggleLabelColumn.implicitHeight + 16)
        opacity: toggleRow.enabled ? 1 : 0.45

        RowLayout {
            anchors.fill: parent
            spacing: 16

            Column {
                id: toggleLabelColumn
                Layout.fillWidth: true
                Layout.alignment: Qt.AlignVCenter
                spacing: 3

                Text {
                    width: parent.width
                    text: toggleRow.title
                    color: Appearance.colors.colOnSurface
                    font.family: Fonts.ui
                    font.pixelSize: 15
                    font.weight: Font.Medium
                }

                Text {
                    width: parent.width
                    text: toggleRow.description
                    color: Appearance.colors.colSubtext
                    font.family: Fonts.ui
                    font.pixelSize: 12
                    wrapMode: Text.WordWrap
                    visible: text !== ""
                }
            }

            StyledSwitch {
                Layout.alignment: Qt.AlignVCenter
                enabled: toggleRow.enabled
                checked: toggleRow.checked
                onToggled: toggleRow.toggled(checked)
            }
        }
    }

    component SliderSettingRow: ColumnLayout {
        id: sliderRow

        property string title: ""
        property string description: ""
        property real value: 0
        property real from: 0
        property real to: 1
        property real stepSize: 1
        property string suffix: ""

        signal moved(real value)

        Layout.fillWidth: true
        spacing: 6

        function formatValue(displayValue) {
            return Math.round(displayValue).toString() + (suffix !== "" ? " " + suffix : "");
        }

        RowLayout {
            Layout.fillWidth: true
            spacing: 16

            Text {
                Layout.fillWidth: true
                Layout.alignment: Qt.AlignVCenter
                text: sliderRow.title
                color: Appearance.colors.colOnSurface
                font.family: Fonts.ui
                font.pixelSize: 15
                font.weight: Font.Medium
            }

            Text {
                Layout.alignment: Qt.AlignVCenter
                text: sliderRow.formatValue(sliderRow.value)
                color: Appearance.colors.colOnSurfaceVariant
                font.family: Fonts.numeric
                font.pixelSize: Typography.bodyMedium.pixelSize
                font.weight: Font.Medium
                horizontalAlignment: Text.AlignRight
                verticalAlignment: Text.AlignVCenter
            }
        }

        Text {
            Layout.fillWidth: true
            text: sliderRow.description
            color: Appearance.colors.colSubtext
            font.family: Fonts.ui
            font.pixelSize: 12
            wrapMode: Text.WordWrap
            visible: text !== ""
        }

        MaterialSlider {
            id: settingSlider

            Layout.fillWidth: true
            Layout.preferredHeight: 72
            from: sliderRow.from
            to: sliderRow.to
            stepSize: sliderRow.stepSize
            value: sliderRow.value
            accessibleName: sliderRow.title
            valueFormatter: sliderValue => Math.round(sliderValue).toString() + sliderRow.suffix
            onMoved: value => sliderRow.moved(Math.round(value))
        }
    }

    ColumnLayout {
        id: contentColumn
        width: root.pageContentWidth
        x: Math.max(24, (root.width - width) / 2)
        y: 28
        spacing: 30

        NiriSetupPrompt {
            Layout.fillWidth: true
            title: I18n.tr("Cursor integration")
            description: I18n.tr("Create or connect the Clavis cursor configuration.")
            integrationState: NiriConfigService.state("cursor")
            busy: NiriConfigService.busy && NiriConfigService.activeFeature === "cursor"
            blocked: NiriConfigService.busy
            error: NiriConfigService.error
            onSetupRequested: NiriConfigService.setup("cursor")
        }

        RowLayout {
            Layout.fillWidth: true
            spacing: 16

            ThemePreviewCard {
                title: I18n.tr("Light")
                mode: "light"
                darkPreview: false
                onClicked: ThemeService.setThemeMode("light")
            }

            ThemePreviewCard {
                title: I18n.tr("Dark")
                mode: "dark"
                darkPreview: true
                onClicked: ThemeService.setThemeMode("dark")
            }
        }

        Section {
            id: searchSectionPanelStyle
            title: searchAnchorPanelStyle.title
            SettingsSearchAnchor {
                id: searchAnchorPanelStyle
                target: searchSectionPanelStyle
                declaration:
                    '{"id":"theme.section.settings-panel-style","route":"theme","title":"Settings panel style","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "dashboard_customize"

            SettingsPanelStyleCard {
                Layout.fillWidth: true
            }
        }

        Section {
            id: searchSection0
            title: searchAnchor0.title
            SettingsSearchAnchor {
                id: searchAnchor0
                target: searchSection0
                declaration:
                    '{"id":"theme.section.matugen-color-scheme","route":"theme","title":"matugen color scheme","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "colors"

            ColumnLayout {
                Layout.alignment: Qt.AlignLeft
                spacing: 4

                StyledButtonGroup {
                    Layout.alignment: Qt.AlignLeft
                    model: PersonalizationConfig.matugenSchemes.slice(0, 5)
                    currentValue: PersonalizationConfig.matugenScheme
                    horizontalPadding: 24
                    onValueSelected: value => ThemeService.setMatugenScheme(value)
                }

                StyledButtonGroup {
                    Layout.alignment: Qt.AlignLeft
                    model: PersonalizationConfig.matugenSchemes.slice(5, 9)
                    currentValue: PersonalizationConfig.matugenScheme
                    horizontalPadding: 24
                    onValueSelected: value => ThemeService.setMatugenScheme(value)
                }
            }
        }

        Section {
            id: searchSectionEffects
            title: searchAnchorEffects.title
            SettingsSearchAnchor {
                id: searchAnchorEffects
                target: searchSectionEffects
                declaration:
                    '{"id":"theme.section.transparency-and-blur","route":"theme","title":"Transparency and blur","context":"ThemePage","icon":"blur_on","aliases":["effects.section.background","general.effects.section.background","effects","general.effects"]}'
            }
            iconName: "blur_on"

            NiriSetupPrompt {
                Layout.fillWidth: true
                title: I18n.tr("Background effects")
                description: I18n.tr("Create or connect the Clavis X-Ray rules.")
                integrationState: NiriConfigService.state("effects")
                busy: NiriConfigService.busy && NiriConfigService.activeFeature === "effects"
                blocked: NiriConfigService.busy
                error: NiriConfigService.error
                onSetupRequested: NiriConfigService.setup("effects")
            }

            SliderSettingRow {
                title: I18n.tr("Background opacity")
                from: 0
                to: 100
                stepSize: 1
                suffix: "%"
                value: PersonalizationConfig.shellBackgroundOpacity * 100
                onMoved: value => PersonalizationConfig.setShellBackgroundOpacity(value / 100)
            }

            ToggleSettingRow {
                title: I18n.tr("Background blur")
                enabled: BlurService.available
                checked: PersonalizationConfig.shellBlurEnabled
                onToggled: checked => PersonalizationConfig.setShellBlurEnabled(checked)
            }

            ToggleSettingRow {
                title: I18n.tr("Blur wallpaper only")
                description: BlurService.niriIntegrationReady ? I18n.tr(
                                                                   "Turning this off also blurs windows and uses more resources") :
                                                               I18n.tr("Configure Niri blur integration first")
                enabled: BlurService.available && BlurService.niriIntegrationReady
                checked: PersonalizationConfig.shellBlurXray
                onToggled: checked => PersonalizationConfig.setShellBlurXray(checked)
            }

            InlineStatusBanner {
                Layout.fillWidth: true
                visible: BlurService.lastError !== ""
                tone: "error"
                message: BlurService.lastError
            }
        }

        Section {
            id: searchSection1
            title: searchAnchor1.title
            SettingsSearchAnchor {
                id: searchAnchor1
                target: searchSection1
                declaration:
                    '{"id":"theme.section.super-key-appearance","route":"theme","title":"Super key appearance","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "keyboard"
            Flow {
                Layout.fillWidth: true
                spacing: Metrics.spacingS
                Repeater {
                    model: PersonalizationConfig.superKeyStyles
                    delegate: RippleButton {
                        id: superChoice
                        required property var modelData
                        implicitWidth: 84
                        implicitHeight: 56
                        buttonRadius: Appearance.rounding.small
                        containerColor: PersonalizationConfig.superKeyStyle === modelData.value
                                        ? Appearance.colors.colSecondaryContainer :
                                          Appearance.colors.colLayer2
                        hoverStateLayerOpacity: 0.08
                        pressedStateLayerOpacity: 0.12
                        focusStateLayerOpacity: 0.12
                        Accessible.name: modelData.label
                        Accessible.role: Accessible.RadioButton
                        Accessible.checked: PersonalizationConfig.superKeyStyle === modelData.value
                        onClicked: PersonalizationConfig.setValue("superKeyStyle", modelData.value)
                        contentItem: Item {
                            ShortcutKeycap {
                                anchors.centerIn: parent
                                keyText: "Super"
                                superStyle: superChoice.modelData.value
                            }
                        }
                        StyledToolTip {
                            text: parent.modelData.label
                            visible: parent.hovered
                        }
                    }
                }
            }
        }

        Section {
            id: searchSection2
            title: searchAnchor2.title
            SettingsSearchAnchor {
                id: searchAnchor2
                target: searchSection2
                declaration:
                    '{"id":"theme.section.lock-screen","route":"theme","title":"Lock screen","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "lock"

            SearchSelectMenuField {
                closeOnAccept: true
                Layout.preferredWidth: 240
                Layout.preferredHeight: 40
                options: PersonalizationConfig.lockScreenStyles
                value: PersonalizationConfig.lockScreenStyle
                textRole: "label"
                valueRole: "value"
                onAccepted: value => PersonalizationConfig.setLockScreenStyle(value)
            }
        }

        Section {
            id: searchSection3
            title: searchAnchor3.title
            SettingsSearchAnchor {
                id: searchAnchor3
                target: searchSection3
                declaration:
                    '{"id":"theme.section.cursor-theme","route":"theme","title":"Cursor theme","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "mouse"

            CursorThemeSelect {
                enabled: NiriConfigService.ready("cursor")
                cursorThemes: ThemeService.availableCursorThemes
                currentCursorTheme: PersonalizationConfig.cursorTheme
                onAccepted: value => ThemeService.setCursorTheme(value)
            }

            InlineStatusBanner {
                Layout.fillWidth: true
                visible: ThemeService.cursorLastError !== ""
                tone: "error"
                message: ThemeService.cursorLastError
            }

            SliderSettingRow {
                enabled: NiriConfigService.ready("cursor")
                title: I18n.tr("Cursor size")
                from: 12
                to: 128
                stepSize: 1
                suffix: I18n.tr("pixels")
                value: PersonalizationConfig.cursorSize
                onMoved: value => ThemeService.setCursorSize(Math.round(value))
            }

            ToggleSettingRow {
                enabled: NiriConfigService.ready("cursor")
                title: I18n.tr("Hide while typing")
                checked: PersonalizationConfig.cursorHideWhenTyping
                onToggled: checked => ThemeService.setCursorHideWhenTyping(checked)
            }

            SliderSettingRow {
                enabled: NiriConfigService.ready("cursor")
                title: I18n.tr("Hide after timeout")
                description: I18n.tr("Hide the cursor after inactivity; 0 disables this")
                from: 0
                to: 5000
                stepSize: 100
                suffix: I18n.tr("milliseconds")
                value: PersonalizationConfig.cursorHideAfterInactiveMs
                onMoved: value => ThemeService.setCursorHideAfterInactiveMs(Math.round(value))
            }
        }

        Section {
            id: searchSection4
            title: searchAnchor4.title
            SettingsSearchAnchor {
                id: searchAnchor4
                target: searchSection4
                declaration:
                    '{"id":"theme.section.icon-theme","route":"theme","title":"Icon theme","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "interests"

            SearchSelectSettingRow {
                title: I18n.tr("Icon theme")
                options: ThemeService.availableIconThemes
                value: PersonalizationConfig.iconTheme
                placeholder: I18n.tr("Choose icon theme")
                onAccepted: value => ThemeService.setIconTheme(value)
            }
        }

        Section {
            id: searchSection5
            title: searchAnchor5.title
            SettingsSearchAnchor {
                id: searchAnchor5
                target: searchSection5
                declaration:
                    '{"id":"theme.section.fonts","route":"theme","title":"Fonts","context":"ThemePage","icon":"palette","aliases":[]}'
            }
            iconName: "text_format"

            SearchSelectSettingRow {
                title: I18n.tr("UI font")
                description: I18n.tr("Regular headings, body text, and controls")
                options: FontService.fontOptions
                value: PersonalizationConfig.uiFontFamily
                placeholder: I18n.tr("Select UI font")
                fieldWidth: 280
                onAccepted: value => PersonalizationConfig.setFontFamily("ui", value)
            }

            SearchSelectSettingRow {
                title: I18n.tr("Monospace font")
                description: I18n.tr("Commands, paths, and technical information")
                options: FontService.fontOptions
                value: PersonalizationConfig.monoFontFamily
                placeholder: I18n.tr("Select monospace font")
                fieldWidth: 280
                onAccepted: value => PersonalizationConfig.setFontFamily("mono", value)
            }

            SearchSelectSettingRow {
                title: I18n.tr("Numeric font")
                description: I18n.tr("Time, percentages, and system values")
                options: FontService.fontOptions
                value: PersonalizationConfig.numericFontFamily
                placeholder: I18n.tr("Select numeric font")
                fieldWidth: 280
                onAccepted: value => PersonalizationConfig.setFontFamily("numeric", value)
            }

            SearchSelectSettingRow {
                title: I18n.tr("Expressive font")
                description: I18n.tr("Expressive visual components such as weather")
                options: FontService.fontOptions
                value: PersonalizationConfig.expressiveFontFamily
                placeholder: I18n.tr("Select expressive font")
                fieldWidth: 280
                onAccepted: value => PersonalizationConfig.setFontFamily("expressive", value)
            }
        }

        Section {
            id: searchSection6
            title: searchAnchor6.title
            SettingsSearchAnchor {
                id: searchAnchor6
                target: searchSection6
                declaration:
                    '{"id":"theme.section.matugen-template-generation","route":"theme","title":"Matugen template generation","context":"ThemePage","icon":"tune","aliases":["advanced.section.matugen-template-generation"]}'
            }
            iconName: "tune"

            Item {
                Layout.fillWidth: true
                implicitHeight: templateActions.implicitHeight

                InlineBusyIndicator {
                    anchors.right: templateActions.left
                    anchors.rightMargin: Metrics.spacingXS
                    anchors.verticalCenter: parent.verticalCenter
                    width: implicitWidth
                    height: implicitHeight
                    busy: ThemeService.generating && ThemeService.generationTemplateId === ""
                }

                RowLayout {
                    id: templateActions

                    anchors.right: parent.right
                    anchors.verticalCenter: parent.verticalCenter

                    IconButton {
                        iconName: "refresh"
                        tooltipText: I18n.tr("Refresh templates")
                        onClicked: MatugenTemplateService.refresh()
                    }
                    ActionButton {
                        text: I18n.tr("Add")
                        iconName: "add"
                        enabled: !MatugenTemplateService.busy && PersonalizationConfig.ready
                        onClicked: templateAddWindow.showWindow()
                    }
                }
            }

            InlineStatusBanner {
                Layout.fillWidth: true
                visible: MatugenTemplateService.error !== ""
                tone: "error"
                message: MatugenTemplateService.error
            }
            InlineStatusBanner {
                Layout.fillWidth: true
                visible: !templateAddWindow.visible && MatugenTemplateService.operationError !== ""
                tone: "error"
                message: MatugenTemplateService.operationError
            }
            InlineStatusBanner {
                Layout.fillWidth: true
                visible: ThemeService.generationError !== "" || ThemeService.externalGenerationError !== ""
                tone: "error"
                message: ThemeService.generationError !== "" ? I18n.tr("Failed to generate Matugen colors") :
                                                               I18n.tr("Some Matugen templates failed to generate")
                StyledToolTip {
                    extraVisibleCondition: errorHover.hovered
                    text: ThemeService.generationError || ThemeService.externalGenerationError
                }
                HoverHandler {
                    id: errorHover
                }
            }

            Repeater {
                model: MatugenTemplateService.templates

                SettingsRow {
                    id: templateRow
                    required property var modelData

                    Layout.fillWidth: true
                    iconName: modelData.valid ? modelData.icon : "error"
                    title: modelData.title
                    supportingText: !modelData.valid ? modelData.error : modelData.origin === "user" ? I18n.tr("User templates") :
                                                                                                       ""

                    trailing: Item {
                        implicitWidth: templateRowActions.implicitWidth
                        implicitHeight: templateRowActions.implicitHeight

                        InlineBusyIndicator {
                            anchors.right: templateRowActions.left
                            anchors.rightMargin: Metrics.spacingXS
                            anchors.verticalCenter: parent.verticalCenter
                            width: implicitWidth
                            height: implicitHeight
                            busy: templateRow.modelData.valid && ThemeService.generating
                                  && ThemeService.generationTemplateId === templateRow.modelData.id
                        }

                        RowLayout {
                            id: templateRowActions

                            anchors.right: parent.right
                            anchors.verticalCenter: parent.verticalCenter

                            Item {
                                visible: templateRow.modelData.hasPostHook
                                implicitWidth: Metrics.controlHeightM
                                implicitHeight: Metrics.controlHeightM
                                Accessible.role: Accessible.StaticText
                                Accessible.name: I18n.tr("Run after each generation: %1").arg(
                                                     templateRow.modelData.postHook)

                                MaterialSymbol {
                                    anchors.centerIn: parent
                                    text: "terminal"
                                    iconSize: Metrics.iconM
                                    color: Appearance.colors.colOnSurfaceVariant
                                }
                                HoverHandler {
                                    id: hookHover
                                }
                                StyledToolTip {
                                    extraVisibleCondition: hookHover.hovered
                                    text: I18n.tr("Run after each generation:\n%1").arg(
                                              templateRow.modelData.postHook)
                                }
                            }
                            IconButton {
                                visible: templateRow.modelData.origin === "user"
                                iconName: "folder_open"
                                tooltipText: I18n.tr("Open template location") + "\n"
                                             + templateRow.modelData.inputPath + "\n" + I18n.tr(
                                                 "Output: %1").arg(templateRow.modelData.outputPath)
                                onClicked: MatugenTemplateService.openLocation(templateRow.modelData)
                            }
                            IconButton {
                                visible: templateRow.modelData.origin === "user"
                                iconName: "delete"
                                tooltipText: I18n.tr("Delete template")
                                enabled: !MatugenTemplateService.busy && !ThemeService.generating
                                         && PersonalizationConfig.ready
                                onClicked: root.requestTemplateDeletion(templateRow.modelData)
                            }
                            StyledSwitch {
                                enabled: templateRow.modelData.valid && !ThemeService.generating &&
                                         !MatugenTemplateService.busy && PersonalizationConfig.ready
                                checked: templateRow.modelData.valid
                                         && PersonalizationConfig.isMatugenTemplateEnabled(
                                             templateRow.modelData.id)
                                Accessible.name: I18n.tr("Enable the %1 Matugen template").arg(
                                                     templateRow.modelData.title)
                                onToggled: ThemeService.setMatugenTemplateEnabled(templateRow.modelData.id,
                                                                                  checked)
                            }
                        }
                    }
                }
            }
        }

        Item {
            Layout.fillWidth: true
            Layout.preferredHeight: 24
        }
    }

    MatugenTemplateAddWindow {
        id: templateAddWindow
        parentModal: root.parentModal
    }

    MaterialDialog {
        id: templateDialog
        anchors.centerIn: Overlay.overlay
        width: Math.min(480, root.width - 32)
        dialogTitle: root.pendingDeleteTemplate ? I18n.tr("Delete “%1”?").arg(
                                                      root.pendingDeleteTemplate.title) : ""
        messageText: I18n.tr("Delete the template and its registration. Keep generated output files.")
        onClosed: root.pendingDeleteTemplate = null
        actionsComponent: Component {
            RowLayout {
                Item {
                    Layout.fillWidth: true
                }
                ActionButton {
                    text: I18n.tr("Cancel")
                    onClicked: templateDialog.close()
                }
                ActionButton {
                    text: I18n.tr("Delete")
                    enabled: !MatugenTemplateService.busy && !ThemeService.generating
                             && PersonalizationConfig.ready
                    onClicked: {
                        if (root.pendingDeleteTemplate)
                            MatugenTemplateService.remove(root.pendingDeleteTemplate.id);
                        templateDialog.close();
                    }
                }
            }
        }
    }
}
