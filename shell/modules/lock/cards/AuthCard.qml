import QtQuick
import QtQuick.Controls
import QtQuick.Controls.Material
import QtQuick.Layouts
import ".."
import qs.shared.theme
import qs.shared.controls
import qs.shared.i18n

FocusScope {
    id: root

    property var context: null
    readonly property var passwordShapeQueue: ["circle"]
    readonly property bool hasText: context && context.currentText.length > 0
    readonly property bool busy: context && context.unlockInProgress
    readonly property bool enterEnabled: hasText && !busy
    readonly property bool enterHovered: frameMouse.containsMouse && frameMouse.mouseX >= enterButton.x
    readonly property bool enterPressed: frameMouse.pressed && frameMouse.mouseX >= enterButton.x

    signal requestUnlock

    Layout.fillWidth: true
    Layout.preferredHeight: Metrics.lockAuthHeight
    function forceAuthFocus() {
        capture.forceAuthFocus();
    }

    Connections {
        target: root.context
        ignoreUnknownSignals: true
        function onShouldReFocus() {
            capture.forceActiveFocus();
        }
    }

    Component.onCompleted: capture.forceActiveFocus()
    onActiveFocusChanged: {
        if (activeFocus)
            capture.forceActiveFocus();
    }

    Rectangle {
        id: inputFrame

        anchors.fill: parent
        color: Appearance.colors.colLayer2
        radius: height / 2
        clip: true

        RippleEffect {
            id: rippleEffect

            anchors.fill: parent
            color: Appearance.colors.colOnSurface
            effectOpacity: Appearance.interaction.rippleOpacity
            shapeRadius: inputFrame.radius
        }

        RowLayout {
            anchors.fill: parent
            anchors.margins: Metrics.spacingXS
            spacing: Metrics.spacingL

            Item {
                Layout.preferredWidth: Metrics.controlHeightXL
                Layout.fillHeight: true

                Item {
                    id: progressHost

                    anchors.centerIn: parent
                    width: 32
                    height: 32

                    BusyIndicator {
                        anchors.centerIn: parent
                        width: parent.width
                        height: parent.height
                        padding: 0
                        running: root.busy
                        opacity: root.busy ? 1 : 0
                        Material.theme: Appearance.m3colors.darkmode ? Material.Dark : Material.Light
                        Material.accent: Appearance.colors.colSecondary

                        Behavior on opacity {
                            NumberAnimation {
                                duration: Appearance.animation.expressiveEffects.duration
                                easing.type: Appearance.animation.expressiveEffects.type
                                easing.bezierCurve: Appearance.animation.expressiveEffects.bezierCurve
                            }
                        }
                    }

                    Text {
                        id: lockIcon

                        anchors.centerIn: parent
                        text: "lock"
                        color: Appearance.colors.colOnSurface
                        font.family: Fonts.materialSymbolsRounded
                        font.pixelSize: 24
                        opacity: root.busy ? 0 : 1

                        Behavior on opacity {
                            NumberAnimation {
                                duration: Appearance.animation.expressiveEffects.duration
                                easing.type: Appearance.animation.expressiveEffects.type
                                easing.bezierCurve: Appearance.animation.expressiveEffects.bezierCurve
                            }
                        }
                    }
                }
            }

            Item {
                Layout.fillWidth: true
                Layout.fillHeight: true
                clip: true

                PasswordCapture {
                    id: capture
                    anchors.fill: parent
                    context: root.context
                    onKeyPressed: event => {
                        if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                            placeholder.animateOnNextShow = false;
                            if (!root.busy)
                                root.requestUnlock();
                        }
                    }
                }

                Connections {
                    function onCurrentTextChanged() {
                        const length = root.context ? root.context.currentText.length : 0;
                        if (length > dotsModel.count)
                            dotsList.bindImplicitWidth();
                        else if (length === 0)
                            placeholder.animateOnNextShow = true;
                        while (dotsModel.count < length)
                            dotsModel.append({});
                        while (dotsModel.count > length)
                            dotsModel.remove(dotsModel.count - 1);
                    }

                    target: root.context
                    ignoreUnknownSignals: true
                }

                Text {
                    id: placeholder

                    property bool animateOnNextShow: true

                    anchors.centerIn: parent
                    text: root.busy ? I18n.tr("Loading…") : I18n.tr("Enter password")
                    color: root.busy ? Appearance.colors.colSecondary : Appearance.colors.colOutline
                    font.family: Fonts.numeric
                    font.pixelSize: 17
                    opacity: root.hasText ? 0 : 1
                    scale: root.hasText ? 0.96 : 1

                    Behavior on opacity {
                        enabled: placeholder.animateOnNextShow

                        NumberAnimation {
                            duration: Appearance.animation.expressiveEffects.duration
                            easing.type: Appearance.animation.expressiveEffects.type
                            easing.bezierCurve: Appearance.animation.expressiveEffects.bezierCurve
                        }
                    }

                    Behavior on scale {
                        NumberAnimation {
                            duration: Appearance.animation.expressiveFastSpatial.duration
                            easing.type: Appearance.animation.expressiveFastSpatial.type
                            easing.bezierCurve: Appearance.animation.expressiveFastSpatial.bezierCurve
                        }
                    }
                }

                ListModel {
                    id: dotsModel
                }

                ListView {
                    id: dotsList

                    readonly property real fullWidth: {
                        if (count === 0)
                            return 0;

                        let width = (count - 1) * spacing + dotSize;
                        for (let i = 0; i < count; ++i) {
                            const item = itemAtIndex(i);
                            width += (item ? item.nonAnimatedWidthScale : 1) * dotSize;
                        }
                        return width;
                    }
                    property int dotSize: 17

                    function bindImplicitWidth() {
                        implicitWidthBehavior.enabled = false;
                        implicitWidth = Qt.binding(() => {
                            return fullWidth;
                        });
                        implicitWidthBehavior.enabled = true;
                    }

                    anchors.centerIn: parent
                    anchors.horizontalCenterOffset: implicitWidth > parent.width ? -(implicitWidth
                                                                                     - parent.width) / 2 : 0
                    implicitWidth: fullWidth
                    implicitHeight: dotSize
                    orientation: ListView.Horizontal
                    spacing: Metrics.spacingS
                    interactive: false
                    model: dotsModel

                    Behavior on implicitWidth {
                        id: implicitWidthBehavior

                        NumberAnimation {
                            duration: Appearance.animation.standard.duration
                            easing.type: Appearance.animation.standard.type
                            easing.bezierCurve: Appearance.animation.standard.bezierCurve
                        }
                    }

                    delegate: Item {
                        id: character

                        required property int index
                        property real nonAnimatedWidthScale: 1

                        implicitWidth: dotsList.dotSize
                        width: implicitWidth
                        height: dotsList.dotSize
                        ListView.onRemove: {
                            appearAnimation.stop();
                            removeAnimation.start();
                        }

                        Rectangle {
                            id: characterShape

                            anchors.centerIn: parent
                            width: dotsList.dotSize * 1.2
                            height: dotsList.dotSize * 1.2
                            radius: width / 2
                            color: Appearance.colors.colOnSurface

                            SequentialAnimation {
                                id: appearAnimation

                                running: true

                                ParallelAnimation {
                                    NumberAnimation {
                                        target: characterShape
                                        property: "opacity"
                                        from: 0
                                        to: 1
                                        duration: Appearance.animation.expressiveEffects.duration
                                        easing.type: Appearance.animation.expressiveEffects.type
                                        easing.bezierCurve: Appearance.animation.expressiveEffects.bezierCurve
                                    }

                                    NumberAnimation {
                                        target: characterShape
                                        property: "scale"
                                        from: 0
                                        to: 1
                                        duration: Appearance.animation.expressiveFastSpatial.duration
                                        easing.type: Appearance.animation.expressiveFastSpatial.type
                                        easing.bezierCurve:
                                            Appearance.animation.expressiveFastSpatial.bezierCurve
                                    }

                                    NumberAnimation {
                                        target: character
                                        property: "implicitWidth"
                                        from: dotsList.dotSize
                                        to: dotsList.dotSize * 1.3
                                        duration: Appearance.animation.expressiveDefaultSpatial.duration
                                        easing.type: Appearance.animation.expressiveDefaultSpatial.type
                                        easing.bezierCurve:
                                            Appearance.animation.expressiveDefaultSpatial.bezierCurve
                                    }

                                    PropertyAction {
                                        target: character
                                        property: "nonAnimatedWidthScale"
                                        value: 1.5
                                    }
                                }

                                PauseAnimation {
                                    duration: Appearance.animation.expressiveEffects.duration * 0.9
                                }

                                ParallelAnimation {
                                    NumberAnimation {
                                        target: characterShape
                                        property: "scale"
                                        to: 2 / 3
                                        duration: Appearance.animation.expressiveFastSpatial.duration
                                        easing.type: Appearance.animation.expressiveFastSpatial.type
                                        easing.bezierCurve:
                                            Appearance.animation.expressiveFastSpatial.bezierCurve
                                    }

                                    NumberAnimation {
                                        target: character
                                        property: "implicitWidth"
                                        to: dotsList.dotSize
                                        duration: Appearance.animation.expressiveDefaultSpatial.duration
                                        easing.type: Appearance.animation.expressiveDefaultSpatial.type
                                        easing.bezierCurve:
                                            Appearance.animation.expressiveDefaultSpatial.bezierCurve
                                    }

                                    PropertyAction {
                                        target: character
                                        property: "nonAnimatedWidthScale"
                                        value: 1
                                    }
                                }
                            }

                            SequentialAnimation {
                                id: removeAnimation

                                PropertyAction {
                                    target: character
                                    property: "ListView.delayRemove"
                                    value: true
                                }

                                ParallelAnimation {
                                    NumberAnimation {
                                        target: characterShape
                                        property: "opacity"
                                        to: 0
                                        duration: Appearance.animation.expressiveEffects.duration
                                        easing.type: Appearance.animation.expressiveEffects.type
                                        easing.bezierCurve: Appearance.animation.expressiveEffects.bezierCurve
                                    }

                                    NumberAnimation {
                                        target: characterShape
                                        property: "scale"
                                        to: 0.5
                                        duration: Appearance.animation.expressiveFastSpatial.duration
                                        easing.type: Appearance.animation.expressiveFastSpatial.type
                                        easing.bezierCurve:
                                            Appearance.animation.expressiveFastSpatial.bezierCurve
                                    }
                                }

                                PropertyAction {
                                    target: character
                                    property: "ListView.delayRemove"
                                    value: false
                                }
                            }

                            Behavior on color {
                                ColorAnimation {
                                    duration: Appearance.animation.expressiveSlowEffects.duration
                                    easing.type: Appearance.animation.expressiveSlowEffects.type
                                    easing.bezierCurve: Appearance.animation.expressiveSlowEffects.bezierCurve
                                }
                            }
                        }
                    }
                }
            }

            Rectangle {
                id: enterButton

                Layout.alignment: Qt.AlignVCenter
                Layout.preferredWidth: implicitWidth + (root.enterPressed ? Metrics.lockOuterPadding * 2 :
                                                                            root.hasText
                                                                            ? Metrics.lockOuterPadding : 0)
                implicitWidth: enterIcon.implicitWidth + Metrics.lockOuterPadding * 2
                implicitHeight: enterIcon.implicitHeight + Metrics.spacingM * 2
                radius: root.hasText || root.enterPressed ? Metrics.cornerL : Math.min(implicitWidth,
                                                                                       implicitHeight) / 2
                color: root.hasText ? Appearance.colors.colPrimary : Appearance.colors.colLayer3

                Rectangle {
                    anchors.fill: parent
                    radius: parent.radius
                    color: root.hasText ? Appearance.colors.colOnPrimary : Appearance.colors.colOnSurface
                    opacity: root.enterPressed ? 0.2 : root.enterHovered ? 0.12 : 0

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Appearance.animation.expressiveEffects.duration
                            easing.type: Appearance.animation.expressiveEffects.type
                            easing.bezierCurve: Appearance.animation.expressiveEffects.bezierCurve
                        }
                    }
                }

                Text {
                    id: enterIcon

                    anchors.centerIn: parent
                    text: "arrow_forward"
                    color: root.hasText ? Appearance.colors.colOnPrimary : Appearance.colors.colOnSurface
                    font.family: Fonts.materialSymbolsRounded
                    font.pixelSize: 24
                    font.weight: 500
                }

                Behavior on Layout.preferredWidth {
                    NumberAnimation {
                        duration: Appearance.animation.expressiveFastSpatial.duration
                        easing.type: Appearance.animation.expressiveFastSpatial.type
                        easing.bezierCurve: Appearance.animation.expressiveFastSpatial.bezierCurve
                    }
                }

                Behavior on radius {
                    NumberAnimation {
                        duration: Appearance.animation.expressiveFastSpatial.duration
                        easing.type: Appearance.animation.expressiveFastSpatial.type
                        easing.bezierCurve: Appearance.animation.expressiveFastSpatial.bezierCurve
                    }
                }

                Behavior on color {
                    ColorAnimation {
                        duration: Appearance.animation.standard.duration
                        easing.type: Appearance.animation.standard.type
                        easing.bezierCurve: Appearance.animation.standard.bezierCurve
                    }
                }
            }
        }

        MouseArea {
            id: frameMouse

            anchors.fill: parent
            z: 10
            hoverEnabled: true
            cursorShape: root.enterEnabled && mouseX >= enterButton.x ? Qt.PointingHandCursor : Qt.IBeamCursor
            onPressed: mouse => {
                rippleEffect.startAt(mouse.x, mouse.y);
                capture.forceActiveFocus();
            }
            onClicked: mouse => {
                capture.forceActiveFocus();
                if (root.enterEnabled && mouse.x >= enterButton.x)
                    root.requestUnlock();
            }
        }
    }
}
